Function debug(txt$)
	Print "aoe("+ GameTime +"): "+txt
EndFunction

Function newPosition(_x#, _y#)
	_position.vector2 = New(vector2)
	_position\x = _x
	_position\y = _y
	Return ConvertToInteger(_position)
EndFunction

Function positionTotileX#(position)
	p.vector2 = ConvertToType(position)
	Return p\x
EndFunction

Function positionTotileY#(position)
	p.vector2 = ConvertToType(position)
	Return p\y
EndFunction

Function loadFrames(_name$, _direction)
	debug(">>> loadFrames("+_name+", "+_direction+", "+mask+")") //DEBUG
	mem = MakeMEMBlock(5)

	dat = OpenToRead(mediadir + _name + "/Image.dat")
	img = LoadImage(mediadir + _name + "/Image.png")

	width = Int(ReadLine(dat))
	height = Int(ReadLine(dat))
	mask = Int(ReadLine(dat))
	wimg = (width+height)*TW2


	if (mask) Then MaskImage img, 255, 0, 255

	framesCount = Int(ImageWidth(img)/wimg)

	debug("    loadFrames("+_name+", "+_direction+", "+mask+") - New frame ("+width+", "+height+", "+framesCount+", "+wimg+")") //DEBUG
	
	PokeByte mem, 0, framesCount
	PokeInt  mem, 1, img
	
	debug("<<< loadFrames("+_name+", "+_direction+", "+mask+")") //DEBUG
	Return mem 'muistipala, jotta voi palauttaa useita arvoja
End Function 

// ANIMAATIOT >>>>>>>>>>>>>>>

Function makeNewAnimation(_name$)
	debug(">>> makeNewAnimation("+_name+")") //DEBUG
	dat = OpenToRead(mediadir + _name + "/Image.dat")
	width = Int(ReadLine(dat))
	height = Int(ReadLine(dat))
	mask = Int(ReadLine(dat))
	_directions$ = ReadLine(dat)
	_speed = Int(ReadLine(dat))
	directions = CountWords(_directions)
	debug("    makeNewAnimation("+_name+") - found "+directions+" directions") //DEBUG
	For i = 0 To directions 
		_newDirection = Int(GetWord(_directions, i))
		newAnimation(loadFrames(_name, _newDirection), _name, _newDirection, _speed)
	Next i
	CloseFile(dat)
	debug("<<< makeNewAnimation("+_name+")") //DEBUG
EndFunction

Function newAnimation(_frames, _name$, _direction, _speed)
	'_frames muistipala, jotta voi palauttaa useita arvoja
	debug(">>> newAnimation("+_frames+", "+_name+", "+_direction+", "+_speed+")") //DEBUG
	_newAnimation.animation		= New(animation)
	_newAnimation\framesImg		= PeekInt(_frames,1)
	_newAnimation\framesCount	= PeekByte(_frames,0)
	_newAnimation\name			= _name
	_newAnimation\speed			= _speed
	_newAnimation\direction		= _direction
	DeleteMEMBlock _frames
	debug("<<< newAnimation("+_frames+", "+_name+", "+_direction+", "+_speed+")") //DEBUG
	Return ConvertToInteger(_newAnimation)
End Function

Function getAnimation(_name$, _direction)
	retmin = 999
	ret = False
	For _animation.animation = Each animation
		If (_animation\name = _name) Then
			If (_animation\direction = _direction) Then
				Return ConvertToInteger(_animation)
			Else 
				If Abs(_animation\direction-_direction) < retmin Then ret = ConvertToInteger(_animation)
			EndIf
		EndIf
	Next _animation
	If (ret = False) then debug("ERROR - getAnimation() failed - animation "+_name+"("+_direction+") Not found") //DEBUG
	Return ret
End Function

// ANIMAATIOT <<<<<<<<<<<<<<<

Function loadEverything()
	debug(">>> loadEverything()") //DEBUG

	debug("    loadEverything() - start loading tiles") //DEBUG
	ChDir "media/tiles"
	StartSearch
	Repeat 
		file$ = FindFile()
		If file = "" Then Exit 'no more files
		If Not (file = "." Or file = "..") Then 
			If (FileExists(tiledir + file + "/Image.dat") And FileExists(tiledir + file + "/Image.png") And FileExists(tiledir + file + "/Prototype.dat")) Then
				makeNewAnimation("tiles/"+file)

				f = OpenToRead(tiledir + file+"/Prototype.dat")
				newTilePrototype("tiles/"+file, Int(ReadInt(f)), Int(ReadInt(f)), getAnimation("tiles/"+file, 1))
				CloseFile(f)
			Else
				debug("aoe: ERROR - loadEverything() failed - could Not load tiles/"+file) //DEBUG
			EndIf
		EndIf
	Forever
	EndSearch
	debug("    loadEverything() - start loading units") //DEBUG
	ChDir "../units"
	StartSearch
	Repeat
		file$ = FindFile()
		If file = "" Then Exit 'no more files
		If Not (file = "." Or file = "..") Then 
			If (FileExists(unitsdir + file+"/Image.dat") And FileExists(unitsdir+file+"/Image.png") And FileExists(unitsdir + file + "/Prototype.dat")) Then
				makeNewAnimation("units/"+file)
				f = OpenToRead(unitsdir + file + "/Prototype.dat")

				newEntityPrototype(Int(ReadLine(f)), "units/"+file, Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), getAnimation("units/"+file, 0),Int(ReadLine(f)), newPosition(Int(ReadLine(f)), Int(ReadLine(f))))
				CloseFile(f)
			Else
				debug("ERROR - loadEverything() failed - could Not load units/"+file) //DEBUG
			EndIf
		EndIf
	Forever
	EndSearch
	debug("    loadEverything() - start loading buildings") //DEBUG
	ChDir "../buildings"
	StartSearch
	Repeat
		file$ = FindFile()
		If file = "" Then Exit 'no more files
		If Not (file = "." Or file = "..") Then 
			If (FileExists(buildingsdir + file+"/Image.dat") And FileExists(buildingsdir + file + "/Image.png") And FileExists(buildingsdir + file + "/Prototype.dat")) Then
				makeNewAnimation("buildings/" + file)

				f = OpenToRead(buildingsdir + file + "/Prototype.dat")

				newEntityPrototype(Int(ReadLine(f)), "buildings/" + file, Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), getAnimation("buildings/"+file, 0),Int(ReadLine(f)), newPosition(Int(ReadLine(f)), Int(ReadLine(f))))
				CloseFile(f)
			Else
				debug("aoe: ERROR - loadEverything() failed - could Not load buildings/"+file+" from "+buildingsdir+file) //DEBUG
			EndIf
		EndIf
	Forever
	EndSearch
	ChDir "../.."
	debug("<<< loadEverything()") //DEBUG
EndFunction

Function drawWorld()
	For z = 0 To 2*MS-1
		For y = 0 To MS-Abs(MS-z)-1 //TODO: use While here
			nx = Max(0,z-MS)+y
			ny = Min(MS,z)-y
'			Print "aoe: drawWorld - ("+z+", "+y+") -> ("+nx+","+ny+")"
			tx = tilexToX(nx, ny)
			ty = tileyToY(nx, ny)

			If (tx > -TW*2 And tx < SW And ty > -TH*2 And ty < (SW-PanelHeight)) Then
				_tile.tile = ConvertToType(map(nx, ny))
				_tilePrototype.tilePrototype = ConvertToType(_tile\prototype)
				_anim.animation = ConvertToType(_tilePrototype\anim)

				DrawImage _anim\framesImg, tx, ty
			EndIf
		Next y
	Next z

	For _entity.entity = Each entity
		mx# = positionTotileX(_entity\position)
		my# = positionTotileY(_entity\position)
	
		tx = tilexToX(mx-2, my)
		ty = tileyToY(mx-2, my)

'		If (tx > -TW*2 And tx < SW And ty > -TH*2 And ty < SW) Then
			_entityPrototype.entityPrototype = ConvertToType(_entity\prototype)
			_anim.animation = ConvertToType(_entityPrototype\anim)
			_position.vector2 = ConvertToType(_entity\position)
			img = _anim\framesImg

			'ALAREUNASTA PIIRTÄÄÄÄÄÄÄ
			DrawImageBox img, tx, ty, ImageWidth(img) / _anim\framesCount * _entity\frame, 0, TW, ImageHeight(img)
			Color 255,255,255
			Text tx, ty, Str(mx)+","+Str(my)+":"+_entity\frame+","+_anim\framesCount+" - "+_anim\speed
'		EndIf
	Next _entity
End Function

Function controls()
	MouX = MouseX()
	MouY = MouseY()

	CamX = CamX - CamSpeed * (KeyDown(32) - KeyDown(30))
	CamY = CamY - CamSpeed * (KeyDown(31) - KeyDown(17))
	
	hit = False
	
	If MouseHit(1) Then
		deleteGroup(PlayerSelectedGroup)
		PlayerSelectedGroup = addGroup()
	ElseIf MouseUp(1) Then
		For _entity.entity = Each entity
			mx# = positionTotileX(_entity\position)
			my# = positionTotileY(_entity\position)
		
			tx = tilexToX(mx-2, my)
			ty = tileyToY(mx-2, my)

			_entityPrototype.entityPrototype = ConvertToType(_entity\prototype)
			_anim.animation = ConvertToType(_entityPrototype\anim)
			img = _anim\framesImg

			tw = ImageWidth(img)/_anim\framesCount
			th = ImageHeight(img)

			If (MouX > tx And MouX < (tx + tw) And MouY > ty And MouY < (ty + th)) Then
				addEntityToGroup(PlayerSelectedGroup, _entity)
				PlayerSelectedGroup = addGroup()
				hit = True
			EndIf
		Next _entity
		If (hit = False) then PlayerSelectedGroup = 0
	EndIf
EndFunction

Function drawGUI()
	h = SH-panelHeight

	Color 120,120,120
	Line 0, h, SW, SH-panelHeight
	Color cbBlackSkin
	Box 0, h+2, SW, SH

	If (PlayerSelect <> 0) Then 
		ps.entity = ConvertToType(PlayerSelect)
		psPrototype.entityPrototype = ConvertToType(ps\prototype)

		psPosition.vector2 = ConvertToType(ps\position)

		Color 255,255,255
		Text 100, h + 10, psPrototype\name
		Text 200, h + 10, "("+psPosition\x+","+psPosition\y+")"
		Text 100, h + 20, "hp: " + ps\hp+"/"+psPrototype\hp
		Text 100, h + 30, "("+psPrototype\attack+", "+psPrototype\defence+", "+psPrototype\armory+", "+psPrototype\range+")"
		Text 100, h + 50, "state: " + ps\state
	EndIf
EndFunction

Function generateMap()
	debug(">>> generateMap()") //DEBUG
	newEntity(getEntityPrototype("buildings/tower"), newPosition(0, 0))
	For x = 0 To MS
		For y = 0 To MS
			prototype = getTilePrototype("tiles/grass")
			map(x, y) = newTile(prototype)
			
			Select(Rand(10)) 
				Case 1
					newEntity(getEntityPrototype("units/soldier"), newPosition(x, y))
				Case 2
					newEntity(getEntityPrototype("units/vieteri"), newPosition(x, y))
				Case 3
					newEntity(getEntityPrototype("buildings/tower"), newPosition(x, y))
				Default
			End Select
		Next y
	Next x
	debug("<<< generateMap()") //DEBUG
End Function

Function updateAI()
	For tEntity.entity = Each entity
		_entityPrototype.entityPrototype = ConvertToType(tEntity\prototype)
		_anim.animation = ConvertToType(_entityPrototype\anim)

		'animaatio
		If _anim\framesCount > 1 And _anim\speed > 0 Then
			framesElapsed# = ((GameTime - tEntity\lastFrameTime) / Float(_anim\speed)) 
			If (framesElapsed > 1) Then
				tEntity\lastFrameTime = GameTime - (framesElapsed - RoundDown(framesElapsed)) * _anim\speed

				tEntity\frame = (tEntity\frame + RoundDown(framesElapsed)) Mod _anim\framesCount
			EndIf
		EndIf
	Next tEntity
EndFunction

Function initMap()
	debug(">>> iniMap()") //DEBUG
	ReDim map(MS, MS)

	CamX = 0
	CamY = 0	
	debug("<<< iniMap()") //DEBUG
End Function
