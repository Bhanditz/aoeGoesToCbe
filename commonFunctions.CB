Function newPosition(_x#, _y#)
	_position.vector2 = New(vector2)
	_position\x = _x
	_position\y = _y
	Return ConvertToInteger(_position)
EndFunction

Function positionTotileX#(position)
	p.vector2 = ConvertToType(position)
	Return p\x
EndFunction

Function positionTotileY#(position)
	p.vector2 = ConvertToType(position)
	Return p\y
EndFunction

Function loadFrames(_name$, _direction, mask)
	Print "aoe: loadFrames("+_name+", "+_direction+", "+mask+")" //DEBUG
	mem = MakeMEMBlock(9)

	dat = OpenToRead("media/" + _name + "/Image.dat")
	img = LoadImage("media/" + _name + "/Image.png")

	width = Int(ReadLine(dat))
	height = Int(ReadLine(dat))
	wimg = (width+height)*TW2

	if (mask) Then MaskImage img, 255, 0, 255

	framesCount = Int(ImageWidth(img)/wimg)
	
	PokeByte mem, 0, framesCount
	PokeByte mem, 1, width
	PokeByte mem, 2, height
	PokeByte mem, 3, wimg
	PokeInt  mem, 4, img
	
	Return mem
End Function 

// ANIMAATIOT >>>>>>>>>>>>>>>

Function newAnimation(_frames, _name$, _direction)
	Print "aoe: newAnimation("+_frames+", "+_name+", "+_direction+")" //DEBUG
	_newAnimation.animation	= New(animation)
	_newAnimation\frames	= _frames
	_newAnimation\name		= _name
	_newAnimation\direction	= _direction

	Return ConvertToInteger(_newAnimation)
End Function

Function getAnimation(_name$, _direction)
	For _animation.animation = Each animation
		//todo: jos ei löydy samaa direction, niin lähin.
		If (_animation\name = _name And _animation\direction = _direction) Then Return ConvertToInteger(_animation)
	Next _animation
	MakeError "aoe: ERROR - getAnimation() failed" //DEBUG
	Return False
End Function

// ANIMAATIOT <<<<<<<<<<<<<<<

Function loadEverything()
	Print "aoe: loadEverything()" //DEBUG

	newAnimation(loadFrames("tiles/grass", 0, 0), "tiles/grass", 0)
	newAnimation(loadFrames("units/soldier", 0, 1), "units/soldier", 0)

	ChDir "media/tiles"
	StartSearch
	Repeat
		file$ = FindFile()
		If file = "" Then Exit 'no more files
		If Not (file = "." Or file = "..") Then 
			If (FileExists(CurrentDir()+file+"/Image.dat") And FileExists(CurrentDir()+file+"/Image.png") And FileExists(CurrentDir()+file+"/Prototype.dat")) Then
				f = OpenToRead(CurrentDir()+file+"/Prototype.dat")
				newTilePrototype("tiles/"+file, Int(ReadInt(f)), Int(ReadInt(f)), getAnimation("tiles/"+file, 0))
			Else
				Print "aoe: ERROR - loadEverything() failed - could Not load tiles/"+file //DEBUG
			EndIf
		EndIf
	Forever
	EndSearch

	ChDir "../units"
	StartSearch
	Repeat
		file$ = FindFile()
		If file = "" Then Exit 'no more files
		If Not (file = "." Or file = "..") Then 
			If (FileExists(CurrentDir()+file+"/Image.dat") And FileExists(CurrentDir()+file+"/Image.png") And FileExists(CurrentDir()+file+"/Prototype.dat")) Then
				f = OpenToRead(CurrentDir()+file+"/Prototype.dat")
				newEntityPrototype(Int(ReadLine(f)), "units/"+file, Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), Int(ReadLine(f)), getAnimation("units/"+file, 0),Int(ReadLine(f)), newPosition(Int(ReadLine(f)), Int(ReadLine(f))))
			Else
				Print "aoe: ERROR - loadEverything() failed - could Not load units/"+file //DEBUG
			EndIf
		EndIf
	Forever
	EndSearch
	ChDir "../.."
EndFunction

Function drawWorld()
	For z = 0 To 2*MS-1
		For y = 0 To MS-Abs(MS-z)-1 //TODO: use While here
			nx = Max(0,z-MS)+y
			ny = Min(MS,z)-y
'			Print "aoe: drawWorld - ("+z+", "+y+") -> ("+nx+","+ny+")"
			tx = tilexToX(nx, ny)
			ty = tileyToY(nx, ny)

			If (tx > -TW*2 And tx < SW And ty > -TH*2 And ty < (SW-PanelHeight)) Then
				_tile.tile = ConvertToType(map(nx, ny))
				_tilePrototype.tilePrototype = ConvertToType(_tile\prototype)
				_anim.animation = ConvertToType(_tilePrototype\anim)

				DrawImage PeekInt(_anim\frames, 4), tx, ty
			EndIf
		Next y
	Next z

	For _entity.entity = Each entity
		mx# = positionTotileX(_entity\position)
		my# = positionTotileY(_entity\position)
	
		tx = tilexToX(mx-2, my)
		ty = tileyToY(mx-2, my)

'		If (tx > -TW*2 And tx < SW And ty > -TH*2 And ty < SW) Then
			_entityPrototype.entityPrototype = ConvertToType(_entity\prototype)
			_anim.animation = ConvertToType(_entityPrototype\anim)

			DrawImage PeekInt(_anim\frames, 4), tx, ty
			Color 255,255,255
			Text tx, ty, Str(mx)+","+Str(my)
'		EndIf
	Next _entity
End Function

Function controls()
	MouX = MouseX()
	MouY = MouseY()

	CamX = CamX - CamSpeed * (KeyDown(32) - KeyDown(30))
	CamY = CamY - CamSpeed * (KeyDown(31) - KeyDown(17))
	
	hit = False
	
	If (MouseUp(1)) Then
		For _entity.entity = Each entity
			mx# = positionTotileX(_entity\position)
			my# = positionTotileY(_entity\position)
		
			tx = tilexToX(mx-2, my)
			ty = tileyToY(mx-2, my)

			_entityPrototype.entityPrototype = ConvertToType(_entity\prototype)
			_anim.animation = ConvertToType(_entityPrototype\anim)
			img = PeekInt(_anim\frames, 4)

			tw = ImageWidth(img)/PeekByte(_anim\frames, 0)
			th = ImageHeight(img)

			If (MouX > tx And MouX < (tx + tw) And MouY > ty And MouY < (ty + th)) Then
				PlayerSelect = ConvertToInteger(_entity)
				hit = True
			EndIf
		Next _entity
		If (hit = False) then PlayerSelect = 0
	EndIf
EndFunction

Function drawGUI()
	h = SH-panelHeight

	Color 120,120,120
	Line 0, h, SW, SH-panelHeight
	Color cbBlackSkin
	Box 0, h+2, SW, SH

	If (PlayerSelect <> 0) Then 
		ps.entity = ConvertToType(PlayerSelect)
		psPrototype.entityPrototype = ConvertToType(ps\prototype)

		psPosition.vector2 = ConvertToType(ps\position)

		Color 255,255,255
		Text 100, h + 10, psPrototype\name
		Text 200, h + 10, "("+psPosition\x+","+psPosition\y+")"
		Text 100, h + 20, "hp: " + ps\hp+"/"+psPrototype\hp
		Text 100, h + 30, "("+psPrototype\attack+", "+psPrototype\defence+", "+psPrototype\armory+", "+psPrototype\range+")"
		Text 100, h + 50, "state: " + ps\state
	EndIf
EndFunction

Function generateMap()
	Print "aoe: generateMap()" //DEBUG
	For x = 0 To MS
		For y = 0 To MS
			prototype = getTilePrototype("tiles/grass")
			map(x, y) = newTile(prototype)
			
			If (Rand(10) = 1) Then newEntity(getEntityPrototype("units/soldier"), newPosition(x, y))
		Next y
	Next x
	newEntity(getEntityPrototype("units/soldier"), newPosition(0, 0))
End Function

Function initMap()
	Print "aoe: iniMap()" //DEBUG
	ReDim map(MS, MS)

	CamX = 0
	CamY = 0	
End Function
